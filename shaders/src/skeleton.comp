#version 460


layout (std140, set = 0, binding = 0) readonly buffer Preimage {
    float preimage[];
};

layout (std140, set = 0, binding = 1) readonly buffer Image {
    float image[];
};

layout (std140, set = 0, binding = 2) readonly buffer Batch {
    uint batch[];
};

layout (std140, set = 0, binding = 3) readonly buffer PermutationPrefactors {
    uint permutationPrefactors[];
};

layout (std140, set = 0, binding = 4) buffer SquaredDeviations {
    float squaredDeviations[];
};

layout (push_constant) uniform PushConstants {
    uint datapoints;
    uint preimageDimensions;
    uint batchSize;
    uint permutations;
    uint expressionSize;
};

layout (local_size_x = LOCAL_SIZE_X, local_size_y = LOCAL_SIZE_Y, local_size_z = 1) in;

uint _prefactor(uint permutationIdx, uint nodeIdx);
float _sample(uint batchIdx, uint dimension);
float _eval(uint batchIdx, uint permutationIdx);

#include "user-function-declarations"

void main() {
    uint batchIdx = gl_GlobalInvocationID.x;
    uint permutationIdx = gl_GlobalInvocationID.y;
    bool skip = batchIdx >= batchSize || permutationIdx >= permutations;

    if (!skip) {
        float result = _eval(batchIdx, permutationIdx);
        float actual = image[batch[batchIdx]];
        float deviation = result - actual;
        float squaredDeviation = deviation * deviation;
        squaredDeviations[permutationIdx * batchSize + batchIdx] = squaredDeviation;
    }
}

uint _prefactor(uint permutationIdx, uint nodeIdx) {
    return permutationPrefactors[permutationIdx * expressionSize + nodeIdx];
}

float _sample(uint batchIdx, uint dimension) {
    return preimage[batch[batchIdx] * preimageDimensions + dimension];
}

#include "user-function-definitions"
#include "eval-function-definition"
